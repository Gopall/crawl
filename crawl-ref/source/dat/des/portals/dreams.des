##############################################################################
#
# The realm of dreams! (aMcNicky)
#
# A visit here is the player's reward for wearing the dreamshard necklace
# and training a fair chunk of evocations.
#
# The twofold intentions of the portal are:
# 1. to give the player a somewhat safer on-demand (but one-time use) portal
#    that they can step into as a very strong 2-turn escape like a ziggurine.
# 2. to provide an good level of loot (less than a trove, more than the
#    average shop) as an additional pay-off.
#
# Slow moving monsters are included, along with a series of transformations,
# mainly for flavour, but also to keep the action moving and ideally make
# the player choose which items to take with them rather than a no brainer
# of 'pick it all up and figure it out later'
#
# Please feel free to add more dreams in line with these principles!
#
##############################################################################
# TODO: add dream maps, perhaps with some new transformations.


default-depth: Dreams

{{
function dreams_setup(e)
    e.tags("no_dump no_item_gen allow_dup no_monster_gen")
    e.orient("encompass")
    e.kfeat("< = exit_dreams")
end
}}

NAME: amcnicky_dreams_1
: dreams_setup(_G)
TAGS: allow_dup no_trap_gen no_monster_gen generate_awake
DESC: fluttering gently through an ogre-filled clearing
WEIGHT: 10
KMONS: 0 = ogre catcher
KMONS: 1 = ogre catcher
SUBST: 0 = 0.
MAP
0ttttttttttttttttttttttt
0.tttttt|tt|t|tt|ttttt.|
0..t.....tt...tt.tttt...
0................ttt....
1..A..................}.
0................ttt....
0..t.....tt...tt.tttt...
0.tttttt|tt|t|tt|ttttt.|
0ttttttttttttttttttttttt
ENDMAP

NAME: amcnicky_dreams_2
: dreams_setup(_G)
TAGS: allow_dup no_trap_gen no_monster_gen generate_awake
DESC: sprinting, squealing through a spriggan restaurant
WEIGHT: 5000
KMONS: 0 = spriggan col:red name:butcher n_suf ; robe . quick blade
KMONS: 1 = spriggan col:white name:diner n_suf ; robe . dagger
MAP
v..1...1......1....1...v
v.mmmmmmmmmmmmmmmmmmmm.v
v.m0..{.............|m.v
v.mmmmmmmmmmmmmmm.mmmm1v
v.m|.......|.m..m.m1...v
v1m..........mmmm.m.vvvv
v.m...............m.vvvv
v.m..........mmmm.m.vvvv
v.m..........m..m.m.vvvv
v1m|.........m1.m|m.vvvv
v.m|.......<.m..m|m.vvvv
v.mmmmmmmmmmmm..mmm.vvvv
v...1..........1....vvvv
vvvvvvvvvvvvvvvvvvvvvvvv
ENDMAP
